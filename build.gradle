plugins {
    id 'java-library'
    id "io.qameta.allure" version "2.9.6"
}

repositories {
    mavenCentral()
}

def allureVersion = "2.17.2",
    junitVersion = "5.8.2",
    slf4jVersion = "1.7.32"

allure {
    report {
        version.set(allureVersion)
    }
    adapter {
        autoconfigure.set(true)
        aspectjWeaver.set(true)
        frameworks {
            junit5 {
                adapterVersion.set(allureVersion)
            }
        }
    }
}

dependencies {
    implementation 'io.qameta.allure:allure-java-commons:2.16.1'
    //noinspection GradlePackageUpdate
    testImplementation(
            "com.codeborne:selenide:5.25.0",
            "io.qameta.allure:allure-selenide:$allureVersion",
            "io.appium:java-client:7.6.0",
            "org.aeonbits.owner:owner:1.0.12",
            "io.rest-assured:rest-assured:4.4.0",
            "org.junit.jupiter:junit-jupiter-api:$junitVersion")
    testRuntimeOnly(
            "org.slf4j:slf4j-simple:$slf4jVersion",
            "org.junit.jupiter:junit-jupiter-engine:$junitVersion")
}

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

tasks.withType(Test) {
    systemProperties(System.getProperties())
    useJUnitPlatform()

    testLogging {
        lifecycle {
//            events "started", "failed"
            events "started", "skipped", "failed", "standard_error", "standard_out"
            exceptionFormat "short"
        }
    }
}

task browserstack(type: Test) {
    systemProperty("deviceHost", System.getProperty("deviceHost","browserstack"))
    useJUnitPlatform {
        includeTags 'browserstack'
    }
}

task local(type: Test) {
    systemProperty("deviceHost", System.getProperty("deviceHost","local"))
    useJUnitPlatform {
        includeTags 'local'
    }
}

task real(type: Test) {
    systemProperty("deviceHost", System.getProperty("deviceHost","real"))
    useJUnitPlatform {
        includeTags 'real'
    }
}

task selenoid(type: Test) {
    systemProperty("deviceHost", System.getProperty("deviceHost","selenoid"))
    useJUnitPlatform {
        includeTags 'selenoid'
    }
}